plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
}

group = 'venture.Aust1n46.chat'
version = '4.0.0'

repositories {
    mavenCentral()
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "bungeecord-repo"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name = "essentialsx-repo"
        url = "https://repo.essentialsx.net/releases/"
    }
    maven {
        name = "velocity"
        url = "https://repo.velocitypowered.com/snapshots/"
    }
    maven {
        name = "papermc"
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = "google-guice"
        url = "https://mvnrepository.com/artifact/com.google.inject/guice"
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-public/"
    }
}

dependencies {
    implementation("io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:2.3.10")
    implementation("me.clip:placeholderapi:2.10.5")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7")
    compileOnly("com.comphenix.protocol:ProtocolLib:5.3.0-SNAPSHOT")
    implementation("com.massivecraft.factions:Factions:3.3.0")
    implementation("com.massivecraft.massivecore:MassiveCore:3.3.0")
    implementation("com.github.TownyAdvanced:Towny:0.96.7.0")
    implementation('net.essentialsx:EssentialsXDiscord:2.19.0') {
        exclude group: 'org.bstats', module: 'bstats-bukkit'
        exclude group: 'net.dv8tion', module: 'JDA'
    }
    implementation("com.google.inject:guice:7.0.0")
    implementation("com.velocitypowered:velocity-api:3.0.1")
    implementation("net.md-5:bungeecord-api:1.20-R0.2-SNAPSHOT")
    implementation("org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly("org.projectlombok:lombok:1.18.22")
    annotationProcessor("org.projectlombok:lombok:1.18.22")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.8.2")
    testImplementation("org.mockito:mockito-inline:4.2.0")
    testImplementation("org.mockito:mockito-junit-jupiter:4.2.0")
    implementation(project(':api'))
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: project.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'velocity-plugin.yml', 'bungee.yml']) {
        expand props
    }
}

shadowJar {
    archiveBaseName = rootProject.name
    archiveClassifier.set('')
    relocate 'com.zaxxer.hikari', 'shaded.com.zaxxer.hikari'
    relocate 'net.md_5.bungee.config', 'shaded.net.md_5.bungee.config'
    relocate 'com.google.inject', 'shaded.com.google.inject'
    relocate 'org.aopalliance', 'shaded.org.aopalliance'
    relocate 'jakarta.inject', 'shaded.jakarta.inject'
}